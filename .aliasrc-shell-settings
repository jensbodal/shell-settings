# vi: ft=zsh

DIR="$(dirname "$(readlink -f "$0")")"
_LOG_PREFIX="${LOG_PREFIX:=}"
LOG_PREFIX="[~/.aliasrc-shell-settings] "

alias cds="cd $SHELL_SETTINGS_DIR"
alias cdg="cd $HOME/github"
alias pbc="pbcopy"
alias gd="git diff"
alias gdh="gd HEAD"
alias gds="gd --staged"
alias gs="git status"
alias hist="history -E"
alias l='ls -la | grep "^d" && ls -la | grep -v "^d"'
alias ll="ls -la"
alias pf="moxi s 'Command finished successfully.' || moxi s 'Command failed'"
alias rp="realpath"
alias s="source ~/.zshrc && hash -r && if type direnv > /dev/null; then direnv reload; fi"
alias shellsettings="pushd $HOME/github/shell-settings"
alias shell-settings="shellsettings"
alias yarn=__yarn

# functions
alias bunx=__bunx
alias mise-install=__mise-install
alias mise-update=__mise-update
alias cdf=__cdf
alias cdt=__cdt
alias dns=__dns
alias ff=__ff
alias fif=__fif
alias git-set-origin=__git-set-origin
alias git-vim-status=__git-vim-status
alias screen=__screen
alias ssh-keygen-secure=__ssh-keygen-secure
alias u=__u

# bash scripts
export BIN_ALIAS_PATH="$DIR/bin-aliases"
export BASH_SCRIPTS_PATH="$DIR/scripts"
export BIN_AI_PATH="$DIR/ai/bin"
export BIN_X86_64_PATH="$DIR/bin/x86_64"
export PATH="$BIN_ALIAS_PATH:$BASH_SCRIPTS_PATH:$BIN_AI_PATH:$PATH"

alias moxi="${BASH_SCRIPTS_PATH}/moxi/main.sh"

# bash script aliases
for f in `find "$BASH_SCRIPTS_PATH" -type f -name "alias_*"`; do
  cmd=`echo $f | sed -E 's#.*alias_(.*)#\1#'`
  toeval="alias $cmd=\"$BASH_SCRIPTS_PATH/alias_$cmd\""
  eval "$toeval"
done

# just helpful if I'm looking for the command and look under disk :)
alias disknnn="nnn"

if [ "$OS" = "darwin" ]; then
  .log "macOS detected: loading $BASH_SCRIPTS_PATH/osx"
  export PATH="$PATH:$BASH_SCRIPTS_PATH/osx"
fi

if [ "$OS" = "proxmox" ]; then
  .log "proxmox detected: loading $BASH_SCRIPTS_PATH/proxmox"
  export PATH="$PATH:$BASH_SCRIPTS_PATH/proxmox"
fi


# quick hack for wsl bin and aichat for now
if uname -a | grep -q microsoft 2>&1; then
  export PATH="$PATH:$BIN_X86_64_PATH/windows"
fi

# node scripts
__add_node_scripts_to_path() {
  local nodedir="$DIR/scripts/node"
  for f in `ls $nodedir`; do
    local filename=$(basename "$nodedir/$f" .mjs)
    alias $filename="$nodedir/$f"
  done
}
__add_node_scripts_to_path

################################################################
# aliasing these to functions breaks functionality
cd() {
  if [ $# -eq 1 -a -f "$@" ]; then
    .log "file found, changing to parent directory"
    cdf "$1"
    return $?
  fi

  if [ "$1" != "-" -a ! -d "$@" ]; then
    .log "no such directory: $@"
    return 1
  fi

  builtin cd "$@" && .log "$OLDPWD --> $PWD"
}

__rmrf() {
  SECONDS=0
  .log "rm -rf $@"
  local volume="$(df -P . | sed -n '$s/[^%]*%[[:blank:]]*//p')"
  local tmp=$(mktemp)
  local tmpFolder=$(basename $tmp)
  local folders=("$@")

  if [ $volume = "/" ]; then
    local trash="/tmp/trash"
  else
    local trash="$volume/tmp/trash"
  fi

  mkdir -p $trash

  if [ "$#" -eq 0 ]; then
    .log "trash: $trash"
    ls -la $trash
    return 0;
  fi

  local trashfolder="$trash/$tmpFolder"
  mkdir -p $trashfolder

  .log "moving "$folders" to $trashfolder"
  mv $folders $trashfolder
  .log "emptying $trash in the background..."

  __rmrfhelper() {
    duration=$SECONDS
    command rm -rf $trash
    .log "$(($duration / 60)) minutes and $(($duration % 60)) seconds elapsed."
  }

  { __rmrfhelper & } 2>/dev/null
}

__rm() {
  if [ "$1" = "-rf" ]; then
    shift
    __rmrf "$@"
    return $?
  fi

  command rm "$@"
}

function findup() {
  local needle="${1}"
  local _DIR="${PWD}"

  while [[ $PWD != '/' && ${PWD##*/} != 'httpdocs' ]]; do
    if [[ -d "$needle" || -f "$needle" ]]; then
      .log "${PWD}/${needle}"
      builtin cd "${_DIR}"
      return 0
    fi

    builtin cd ..
  done

  builtin cd "${_DIR}"
}
################################################################

__bunx() {
  if exists bun; then
    bun x "$@"
  else
    mise-install bun && bun x "$@"
  fi
}

__mise-install() {
  if [ -z $1 ]; then
    .log "missing plugin name"
    return
  fi

  mise plugin add "$1"
  mise install "$1@latest"
  mise global "$1@latest"
}

__mise-update() {
  mise update
  for tool in $(mise plugin list); do
    mise install "${tool}" latest  \
      &&  mise global "${tool}" latest  \
      &&  .log "${tool} set globally"
    done
}

__caff() {
  if command -v caffeinate &> /dev/null
  then
    caffeinate -disu
  else
    .log "caffeinate not supported"
  fi
}

__cdf() {
  cd "$(dirname "$1")"
}


# cd to file or folder
__cdt() {
  while [[ $PWD != '/' && `basename $PWD` != "$1" && ! -f "$1" ]]; do cd ..; done
}

__dns() {
  domain="$1"
  .log "[nameservers]"
  dig +short "$domain" ns
  .log "[a records]"
  dig +short "$domain" a
  .log "[cname records]"
  dig +short "$domain" cname
  .log "[txt records]"
  dig +short "$domain" txt
  .log "[mx records]"
  dig +short "$domain" mx
  .log "[aaaa records]"
  dig +short "$domain" aaaa
  .log "[srv records]"
  dig +short "$domain" srv
}

# find file
__ff() {
  local needle="${1:- }"
  local grepopts="$2"

  find . -iname "*$needle*" -type f | grep "$needle" $grepopts
}

# find in file
__fif() {
  grep "$@" -r . | grep "$@"
}

__git-set-origin() {
  local remote="${1:-origin}"
  local currentbranch=$(git rev-parse --abbrev-ref HEAD)

  local gitpath="$remote/$currentbranch"
  .log "Setting upstream to \"$gitpath\""
  git branch --set-upstream-to="$gitpath"
}

__git-vim-status() {
  vim -p $(git status -s | sed -r 's#^(.*->)?[ARMU? ]+(.*)$#\2#')
}

__screen() {
  if [[ ! $1 =~ ^- ]]; then
    if [[ $1 == "ls" ]]; then
      command screen -ls
      return $?
    fi

    command screen -RR -S "$1"
    return $?
  fi

  command screen "$@"
  return $?
}

__ssh-keygen-secure() {
  ${BASH_SCRIPTS_PATH}/generate-ssh-key "${@}"
}

__u() {
  if exists brew; then
    .log "updating brew"
    brew update && brew upgrade
  fi
  if exists pacman; then
    .log "updating pacman"
    sudo pacman -Syu
  fi
  if exists apt; then
    if apt -v &> /dev/null; then
      .log "updating apt"
      sudo apt update && sudo apt upgrade
    fi
  fi
  if exists yum; then
    .log "updating yum"
    sudo yum update
  fi
}


__yarn() {
  .log "!!! use bun !!!"
  return 1
}

LOG_PREFIX=$_LOG_PREFIX

